\Chapter{Strongly Shoda pairs and Simple Algebras}

In this chapter we introduce functions to compute SSPs of a finite group and the simple 
algebras associated. In Section "Strongly Shoda pairs" we introduce a function that computes a 
complete and not redundant list of SSPs of a finite group and another function which computes 
the PCIs realizable by a list of SSPs. In Section "Simple Algebras" we present two functions 
to compute the data defining the simple algebras of the form $\Q Ge(G,K,H)$ for $(K,H)$ a SSP 
of $G$, as explained in Chapter "Introduction". 

In this chapter <Group> stands for a finite group, <RGA> for the rational group algebra of 
<Group>, <ListOfPairsOfSubgs> for a list of pairs $(K,H)$ of subgroups of <Group> such 
that $H$ is a normal subgroup of $K$, and <Subg1> and <Subg2> for two subgroups of <Group> 
such that (<Subg1>,<Subg2>) is a SSP of <Group>. 

 

\Section{Strongly Shoda pairs}

\>StronglyShodaPairs( <RGA> ) F

The function `StronglyShodaPairs' returns a complete and not redundant list of SSPs $(K,H)$ of 
$G$, the underlying finite group of <RGA>. Notice that the output of `StronglyShodaPairs' is 
in general not the list of all SSPs of $G$ (see Chapter "Introduction"). 

\beginexample
    gap> D10:=DihedralGroup(10); QD10:=GroupRing( Rationals, D10 );
    <pc group of size 10 with 2 generators>
    <algebra-with-one over Rationals, with 2 generators>
    gap> SSPD10:=StronglyShodaPairs( QD10 );
    [ [ <pc group of size 10 with 2 generators>, Group([ f2, f1 ]) ],
    [ <pc group of size 10 with 2 generators>, Group([ f2 ]) ],
    [ Group([ f2 ]), Group([  ]) ] ]
\endexample

A warning is showed if the output of `StronglyShodaPairs' is not enough to realize all the 
PCIs of <RGA>. See the last example of "PCIsFromShodaPairs" for the notation in next example. 

\beginexample
    gap> StronglyShodaPairs(QSL23);
    Caution! Some primitive central idempotents are not realizable by strongly Sho\
    da pairs
    [ [ SL(2,3), SL(2,3) ],
    [ SL(2,3), Group([ [ [ Z(3), 0*Z(3) ], [ 0*Z(3), Z(3) ] ],
            [ [ 0*Z(3), Z(3) ], [ Z(3)^0, 0*Z(3) ] ],
            [ [ Z(3)^0, Z(3)^0 ], [ Z(3)^0, Z(3) ] ] ]) ],
    [ Group([ [ [ Z(3), 0*Z(3) ], [ 0*Z(3), Z(3) ] ],
            [ [ 0*Z(3), Z(3) ], [ Z(3)^0, 0*Z(3) ] ],
            [ [ Z(3), Z(3)^0 ], [ Z(3)^0, Z(3)^0 ] ] ]),
        Group([ [ [ Z(3), 0*Z(3) ], [ 0*Z(3), Z(3) ] ],
            [ [ 0*Z(3), Z(3) ], [ Z(3)^0, 0*Z(3) ] ] ]) ] ]
\endexample



\>eGKHsFromKHs(<RGA> , <ListOfPairsOfSubgs>)F

The function `eGKHsFromKHs' returns the list of elements of <RGA> of the form 
$e(G,K,H)$ with $(K,H)$ running on <ListOfPairsOfSubgs>. Though `eGKHsFromKHs' 
works with the only requirement of $H$ being normal in $K$ for every entry $(K,H)$ 
of <ListOfPairsOfSubgs>, the function is meant to be used for <ListOfPairsOfSubgs> a 
list of SSPs of $G$. In that case the output of `eGKHsFromKHs' 
is a list of PCIs of $\Q G$. 

See the first example of "StronglyShodaPairs" for the notation in next example.

\beginexample
    gap> Idemp:=eGKHsFromKHs(QD10,SSPD10);
    [ (1/10)*<identity> of ...+(1/10)*f1+(1/10)*f2+(1/10)*f1*f2+(1/10)*f2^2+(1/
        10)*f1*f2^2+(1/10)*f2^3+(1/10)*f1*f2^3+(1/10)*f2^4+(1/10)*f1*f2^4,
    (1/10)*<identity> of ...+(-1/10)*f1+(1/10)*f2+(-1/10)*f1*f2+(1/10)*f2^2+(-1/
        10)*f1*f2^2+(1/10)*f2^3+(-1/10)*f1*f2^3+(1/10)*f2^4+(-1/10)*f1*f2^4,
    (4/5)*<identity> of ...+(-1/5)*f2+(-1/5)*f2^2+(-1/5)*f2^3+(-1/5)*f2^4 ]
    gap> Id:=PCIsFromSSP(QD10);
    [ (1/10)*<identity> of ...+(1/10)*f1+(1/10)*f2+(1/10)*f1*f2+(1/10)*f2^2+(1/
        10)*f1*f2^2+(1/10)*f2^3+(1/10)*f1*f2^3+(1/10)*f2^4+(1/10)*f1*f2^4,
    (1/10)*<identity> of ...+(-1/10)*f1+(1/10)*f2+(-1/10)*f1*f2+(1/10)*f2^2+(-1/
        10)*f1*f2^2+(1/10)*f2^3+(-1/10)*f1*f2^3+(1/10)*f2^4+(-1/10)*f1*f2^4,
    (4/5)*<identity> of ...+(-1/5)*f2+(-1/5)*f2^2+(-1/5)*f2^3+(-1/5)*f2^4 ]
    gap> Id=Idemp;
    true    
\endexample
    
When <ListOfPairsOfSubgs> is not a list of SSPs, then the entries of the output of 
`eGKHsFromKHs' may not be PCIs of <RGA> nor even idempotents. 
\beginexample
    gap> K:=Subgroup(D10,[D10.1]);
    Group([ f1 ])
    gap> H:=Subgroup(D10,[]);list:=[[K,H]];
    Group([  ])
    [ [ Group([ f1 ]), Group([  ]) ] ]
    gap> Idemp:=eGKHsFromKHs(QD10,list);
    [ (5/2)*<identity> of ...+(-1/2)*f1+(-1/2)*f1*f2+(-1/2)*f1*f2^2+(-1/2)*f1*f2^
        3+(-1/2)*f1*f2^4 ]
    gap> Idemp[1]^2=Idemp[1];
    false
    gap> list:=[[D10,K]];
    [ [ <pc group of size 10 with 2 generators>, Group([ f1 ]) ] ]
    gap> eGKHsFromKHs(QD10,list);
    The second entry of the 1-th element of the list is not
    a normal subgroups of the first one
    fail
\endexample

\Section{Simple Algebras}

\>SimpleAlgebraFromSSP(<Group> , <Subg1> , <Subg2>)F

The function `SimpleAlgebraFromSSP' computes the data describing the simple algebra 
$\Q Ge$, where $G=$ <Group>, for a SSP (<Subg1> , <Subg2>) of <Group>. 
The output of `SimpleAlgebraFromSSP' is a 4-tuple of the form 
    $$[n,k,[[o_i,\alpha_i,\beta_i]:i=1,\ldots,m],[\gamma_{i,j}:1\le i \< j \le 
    m]]$$
where $n=[<Group>:N]$, $k=[<Subg1>:<Subg2>]$, $N$ is the normalizer of <Subg2> in 
<Group> and the meaning of the other data are as explained in Chapter "Introduction" 
for $G=<Group>$, $K=<Subg1>$ and $H=<Subg2>$.

\beginexample
    gap> S4:=SymmetricGroup(4);
    Sym( [ 1 .. 4 ] )
    gap> A4:=AlternatingGroup(4);
    Alt( [ 1 .. 4 ] )
    gap> SimpleAlgebraFromSSP(S4,S4,A4);
    [ 1, 2, [  ], [  ] ]
\endexample

In the previous example one has $n=1$, $k=2$ and the third and four 
entries of the output are empty lists, so that $N=S_4$, $\Q(\xi_k)=\Q$ and 
the simple algebra $\Q Ge(S_4,A_4,A_4)$ is isomorphic to $\Q$. 
\beginexample
    gap> H:=Subgroup(S4,[(1,2)(3,4),(1,3)(2,4)]);
    Group([ (1,2)(3,4), (1,3)(2,4) ])
    gap> SimpleAlgebraFromSSP(S4,A4,H);
    [ 1, 3, [ [ 2, 2, 0 ] ], [ [  ] ] ]
\endexample
In the previous example $n=1$ and $k=3$, so that if $e=e(S_4,A_4,H)$ then 
$\Q Ge\simeq \Q(\xi_3)*S_4/A_4$. 
Moreover the list $\{(o_i,\alpha_i,\beta_i):i=1,\ldots,m\}$ is [ [ 2, 2, 0 ] ], 
which has only one element because $S_4/A_4$ is cyclic of order 2. Furthermore there 
is a generator $x$ of $A_4/H$ and an element $y\in S_4/A_4$ such that $yA_4$ is generator of 
$N/K$ and the second and third entry of [ 2, 2, 0 ] means that $x^y= x^2$ and 
$y^2=1$. Thus $\Q Ge$ is isomorphic to the algebra given by the next 
presentation $A=\Q(X,Y: X^2+X+1=0,XY=YX^2,Y^2=1)$, where $X=xe$ and $Y=ye$. Thus 
$A$ is a quaternion algebra over $\Q$ with a non trivial zero divisor (note that 
$(Y+1)(Y-1)=0$) and therefore $\Q S_4 e$ is isomorphic to $M_2(\Q)$. 

\>SimpleFactorsFromListOfSSP(<Group>, <ListOfPairsOfSubgs>)F

The function `SimpleFactorsFromListOfSSP' applies `SimpleAlgebraFromSSP' to the elements of 
the form (<Group> , <Subg1> , <Subg2>) where (<Subg1> , <Subg2>) runs on the entries of 
<ListOfPairsOfSubgs>.


\beginexample
    gap> QS4:=GroupRing(Rationals,S4);
    <algebra-with-one over Rationals, with 2 generators>
    gap> KHs:=StronglyShodaPairs(QS4);
    [ [ Sym( [ 1 .. 4 ] ), Group([ (1,3)(2,4), (1,4)(2,3), (2,4,3), (1,2) ]) ],
    [ Sym( [ 1 .. 4 ] ), Group([ (1,3)(2,4), (1,4)(2,3), (2,4,3) ]) ],
    [ Group([ (1,3)(2,4), (1,4)(2,3), (1,2) ]),
        Group([ (1,2)(3,4), (1,4,2,3) ]) ],
    [ Group([ (1,3)(2,4), (1,4)(2,3), (1,2) ]), Group([ (1,2)(3,4), (1,2) ]) ],
    [ Group([ (1,3)(2,4), (1,4)(2,3), (2,4,3) ]),
        Group([ (1,4)(2,3), (1,3)(2,4) ]) ] ]
    gap> SimpleFactorsFromListOfSSP(S4,KHs);
    [ [ 1, 1, [  ], [  ] ], [ 1, 2, [  ], [  ] ], [ 3, 2, [  ], [  ] ],
    [ 3, 2, [  ], [  ] ], [ 1, 3, [ [ 2, 2, 0 ] ], [ [  ] ] ] ]
\endexample

In the previous example one has computed a complete and not redundant list of SSPs of $S_4$ and 
one has applied `SimpleFactorsFromListOfSSP' to this list. Two of the data obtained were 
already interpreted in "SimpleAlgebraFromSSP". The interpretation of the other data leads to 
the well known decomposition $\Q S_4 \simeq 2\Q \oplus M_2(\Q) \oplus 2 M_3(\Q)$. 

Now we show how to compute the Wedderburn decomposition of the following four groups 
of order 16:
    $$\matrix{
    D_{16}   &=& \langle a,b | a^8=b^2=1, bab^{-1}=a^{-1} \rangle ,\cr
    D_{16}^- &=& \langle a,b | a^8=b^2=1, bab^{-1}=a^3 \rangle ,\cr
    D_{16}^+ &=& \langle a,b | a^8=b^2=1, bab^{-1}=a^5 \rangle ,\cr    
    Q_{16}   &=& \langle a,b | a^8=b^2a^4=1, bab^{-1}=a^{-1} \rangle.    
    }$$
    
\beginexample
    gap> f:=FreeGroup("a","b");a:=f.1;b:=f.2;
    <free group on the generators [ a, b ]>
    a
    b
    gap> D16:=f/[a^8,b^2,b*a*b^-1*a];;QD16:=GroupRing(Rationals,D16);;
    gap> D16m:=f/[a^8,b^2,b*a*b^-1*a^-3];;QD16m:=GroupRing(Rationals,D16m);;
    gap> D16p:=f/[a^8,b^2,b*a*b^-1*a^-5];;QD16p:=GroupRing(Rationals,D16p);;
    gap> Q16:=f/[a^8,b^2*a^4,b*a*b^-1*a];;QQ16:=GroupRing(Rationals,Q16);;
    gap> FD16:=SimpleFactorsFromListOfSSP(D16,StronglyShodaPairs(QD16));
    [ [ 1, 1, [  ], [  ] ], [ 1, 2, [  ], [  ] ], [ 1, 2, [  ], [  ] ],
    [ 1, 2, [  ], [  ] ], [ 2, 2, [  ], [  ] ], 
    [ 1, 8, [ [ 2, 7, 0 ] ], [  ] ] ]
    gap> FD16m:=SimpleFactorsFromListOfSSP(D16m,StronglyShodaPairs(QD16m));
    [ [ 1, 1, [  ], [  ] ], [ 1, 2, [  ], [  ] ], [ 1, 2, [  ], [  ] ],
    [ 1, 2, [  ], [  ] ], [ 2, 2, [  ], [  ] ], 
    [ 1, 8, [ [ 2, 3, 0 ] ], [  ] ] ]
    gap> FD16p:=SimpleFactorsFromListOfSSP(D16p,StronglyShodaPairs(QD16p));
    [ [ 1, 1, [  ], [  ] ], [ 1, 2, [  ], [  ] ], [ 1, 2, [  ], [  ] ],
    [ 1, 2, [  ], [  ] ], [ 1, 4, [  ], [  ] ], [ 1, 4, [  ], [  ] ],
    [ 2, 4, [  ], [  ] ] ]
    gap> FQ16:=SimpleFactorsFromListOfSSP(Q16,StronglyShodaPairs(QQ16));
    [ [ 1, 1, [  ], [  ] ], [ 1, 2, [  ], [  ] ], [ 1, 2, [  ], [  ] ],
    [ 1, 2, [  ], [  ] ], [ 2, 2, [  ], [  ] ], 
    [ 1, 8, [ [ 2, 7, 4 ] ], [  ] ] ]
\endexample

An appropriate interpretation leads to the following isomorphisms:
    $$\matrix{
    \Q D_{16}&\simeq & 4\Q \oplus M_2(\Q) \oplus M_2(\Q(\sqrt{2})), \cr
    \Q D_{16}^-&\simeq & 4\Q \oplus M_2(\Q) \oplus M_2(\Q(\sqrt{-2})), \cr
    \Q D_{16}^+&\simeq & 4\Q \oplus 2\Q(i) \oplus M_2(\Q(i)), \cr
    \Q Q_{16}&\simeq & 4\Q \oplus M_2(\Q) \oplus H(\Q(\sqrt{2})),
    }$$
where $H(F)$ denotes the Hamiltonian quaternion algebra over a field $F$.

We end this section with an example of a rational group algebra with a simple factor 
which is not a cyclic algebra.

\beginexample
    gap> f:=FreeGroup("a","b","c");; a:=f.1;;b:=f.2;;c:=f.3;;
    gap> G:=f/[a^12,b^2*a^6,c^2*a^6,b^-1*a*b*a,c^-1*a*c*a^5,
    a^9*b^-1*c^-1*b*c];;
    gap> QG:=GroupRing(Rationals,G);
    <algebra-with-one over Rationals, with 3 generators>
    gap> SimpleFactorsFromListOfSSP(G,StronglyShodaPairs(QG));
    [ [ 1, 1, [  ], [  ] ], [ 1, 2, [  ], [  ] ], [ 1, 2, 
    [  ], [  ] ], [ 1, 2, [  ], [  ] ], [ 2, 2, [  ], [  ] ], 
    [ 1, 3, [ [ 2, 2, 0 ] ], [  ] ], [ 2, 6, [  ], [  ] ], 
    [ 1, 6, [ [ 2, 5, 0 ] ], [  ] ],[ 1, 8, [ [ 2, 7, 4 ] ], 
    [  ] ], [ 1, 12, [ [ 2, 11, 6 ], [ 2, 7, 6 ] ], [ [ 3 ] ] 
    ] ]
\endexample

The group $G$ of the previous example has the following presentation:
    $$G= \langle a,b,c|a^{12}=a^6b^2=a^6c^2=1,a^b=a^{-1},a^c=a^7,b^c=ba^3 \rangle$$
and an interpretation of the output of the last computation leads to the following 
decomposition of $\Q G$ as a direct product of simple algebras:
    $$\Q G = 4\Q \oplus 3M_2(\Q)\oplus M_2(\Q(\xi_3)) \oplus H(\Q(\sqrt{2})) \oplus A.$$
where
    $$A=\Q(\xi_{12})(g,h|\xi_{12}g=g\xi_{12}^{-1},\xi_{12}h=-h\xi_{12},hg=ghi,g^2=h^2=-1).$$
For more examples of the use of `SimpleFactorsFromListOfSSP' see 
\cite{OR}.
